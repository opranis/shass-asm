; This is the solution for homework #5.
; This subroutine implements the Euclid's algorithm
; for computing the greatest common divisor of 2 unsigned integers.
; The 2 values are initially stored in memory addresses 00 and 01.
; This program destroys the initial values at these addresses,
; and returns the output in the accumulator, and memory address 00.
;
; This code was tested with the values listed at the bottom of the file.
;
; Revision History
;     13 Feb 22  Olivers Pranis     Solution code


;0000        Zero
             ;if either value is 0, gcd is 0
0000  8001;    LDD   b     ;load b into acc
0001  3300;    CMPI  0     ;compare b with 0
0002  9F16;    JE    EndN  ;if is equal, end the program without loading again
0003  1F80;    NOP         ;
0004  8000;    LDD   a     ;load a into acc
0005  3300;    CMPI  0     ;compare a with 0
0006  9F12;    JE    EndN  ;if is equal, end the program without loading again

;0007        Start:
0007  8000;    LDD   a     ;load a
0008  1001;    SUB   b     ;and subtract b
0009  8F03;    JB    Swap  ;if a ended up being smaller, swap the values
000A  1F80;    NOP         ;
000B  A000;    STD   a     ;otherwise, store the value into a
000C  C007;    JMP   Start ;and start from beginning again

;000D        Swap:
            ;Swap a and b:
000D  8000;    LDD   a		;get a
000E  0780;	   TAX			;and put it in X temporarily
000F  8001;    LDD   b		;now get b
0010  A000;    STD   a		;and store it in a
0011  6701;    TXA			;get original a back
0012  A001;    STD   b		;and store it in b
            ;Test if b == 0:
0013  8001;    LDD   b     ;load b into acc
0014  3300;    CMPI  0     ;compare b with 0
0015  9F02;    JE    End   ;if is equal, end the program
0016  1F80;    NOP         ;
0017  C007;    JMP   Start ;otherwise, if b is not 0, jump to beginning

;0018        End:
0018  8000;    LDD   a     ;load a into acc, which is now the GCD
;0019        EndN:
0019  1F00;    RTS         ;return

;Variables
;00 a
;01 b

;Test Cases:
;75 and 87 = 9
;87 and 75 = 9
;0 and 0   = 0
;0 and 16  = 0
;16 and 0  = 0
;1 and FF  = 1
;94 and 94 = 94
;11 and FB = 1
;19 and 7D = 19
;19 and 23 = 5